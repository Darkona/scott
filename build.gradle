plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.4'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.darkona'
version = '0.0.1'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

tasks.register("copyAgents", Copy) {
    from configurations.runtimeClasspath.filter {
        it.name.startsWith("aspectjweaver")
    }
    into project.layout.buildDirectory.dir("libs")
    rename { "aspectjweaver.jar" }
}

build {
    finalizedBy("copyAgents")
}
dependencies {
    implementation "io.github.darkona:logged:1.1.0"
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    //runtimeOnly "org.aspectj:aspectjrt:1.9.24"
    runtimeOnly "org.aspectj:aspectjweaver:1.9.24"
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

bootRun {
    def aj = configurations.runtimeClasspath
            .filter { it.name.startsWith("aspectjweaver") }
            .singleFile
    println aj
    jvmArgs "-javaagent:${aj.absolutePath}"
}
